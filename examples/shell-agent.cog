# Shell agent with sandboxed shell, file read/write
flow shell(command: String) -> String:
    "Execute a sandboxed shell command. Output limited to 50 lines."
    allowed = ["ls", "cat", "grep", "date", "echo", "wc", "pwd", "whoami", "head", "tail"]
    cmd = command.split(" ")[0]
    if not allowed.contains(cmd):
        return f"Error: '{cmd}' is not allowed"
    return __exec_shell__(command + " | head -50")

flow read_file(path: String) -> String:
    "Read the contents of a file"
    content = read(file(path))
    return content.truncate(3000)

flow write_file(path: String, content: String) -> String:
    "Write content to a file"
    write(file(path), content)
    return f"Written {content.length} chars to {path}"

flow main():
    sonnet= "claude-sonnet-4-20250514"
    llama7= "qwen2.5:7b"
    history = []
    tools = ["shell", "read_file", "write_file"]
    system = "You are a helpful assistant with shell and file access. Use tools to answer questions. Be concise."
    write(stdout, "Agent ready. I can run commands, read and write files. Type 'quit' to exit.")
    loop:
        input = read()
        if input == "quit":
            break
        history = history + [f"User: {input}"]
        context = history.join("\n")
        response = think(context, model=sonnet, tools=tools, system=system)
        if response.has_tool_calls:
            response = exec(response, tools=tools)
            answer = think(context + "\n" + response.content, model=sonnet, system=system)
            history = history + [f"Assistant: {answer}"]
            write(stdout, answer)
        else:
            history = history + [f"Assistant: {response.content}"]
            write(stdout, response.content)
